/**
 * \file
 *
 * \brief Exception and interrupt vectors mapping for the INTC Software Driver.
 *
 * Copyright (c) 2009-2011 Atmel Corporation. All rights reserved.
 *
 * \asf_license_start
 *
 * \page License
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice,
 *    this list of conditions and the following disclaimer.
 *
 * 2. Redistributions in binary form must reproduce the above copyright notice,
 *    this list of conditions and the following disclaimer in the documentation
 *    and/or other materials provided with the distribution.
 *
 * 3. The name of Atmel may not be used to endorse or promote products derived
 *    from this software without specific prior written permission.
 *
 * 4. This software may only be redistributed and used in connection with an
 *    Atmel microcontroller product.
 *
 * THIS SOFTWARE IS PROVIDED BY ATMEL "AS IS" AND ANY EXPRESS OR IMPLIED
 * WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT ARE
 * EXPRESSLY AND SPECIFICALLY DISCLAIMED. IN NO EVENT SHALL ATMEL BE LIABLE FOR
 * ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
 * ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 *
 * \asf_license_stop
 *
 */

#if __CORE__ != __AVR32A__ && __CORE__ != __AVR32B__
  #error Implementation for the AVR32 architecture only.
#endif


#include <avr32/io.h>


//! @{
//! \verbatim


// Start of Exception Vector Table.

	/*
	 * EVBA must be aligned with a power of two strictly greater than the
	 * EVBA-relative offset of the last vector.
	 */
	COMMON  EVTAB:CODE:ROOT(9)


	// Force EVBA initialization.
	EXTERN  ??init_EVBA
	REQUIRE ??init_EVBA

	// Export symbol.
	PUBLIC  ??EVBA
	PUBLIC  _evba
??EVBA:
_evba:

	ORG 0x000
	// Unrecoverable Exception.
_handle_Unrecoverable_Exception:
	rjmp $

	ORG 0x004
	// TLB Multiple Hit.
_handle_TLB_Multiple_Hit:
	rjmp $

	ORG 0x008
	// Bus Error Data Fetch.
_handle_Bus_Error_Data_Fetch:
	rjmp $

	ORG 0x00C
	// Bus Error Instruction Fetch.
_handle_Bus_Error_Instruction_Fetch:
	rjmp $

	ORG 0x010
	// NMI.
_handle_NMI:
	lddpc   pc, __eic_nmi_handler

	ORG 0x014
	// Instruction Address.
_handle_Instruction_Address:
	rjmp $

	ORG 0x018
	// ITLB Protection.
_handle_ITLB_Protection:
	rjmp $

	ORG 0x01C
	// Breakpoint.
_handle_Breakpoint:
	rjmp $

	ORG 0x020
	// Illegal Opcode.
_handle_Illegal_Opcode:
	rjmp $

	ORG 0x024
	// Unimplemented Instruction.
_handle_Unimplemented_Instruction:
	rjmp $

	ORG 0x028
	// Privilege Violation.
_handle_Privilege_Violation:
	rjmp $

	ORG 0x02C
	// Floating-Point: UNUSED IN AVR32UC and AVR32AP.
_handle_Floating_Point:
	rjmp $

	ORG 0x030
	// Coprocessor Absent: UNUSED IN AVR32UC.
_handle_Coprocessor_Absent:
	rjmp $

	ORG 0x034
	// Data Address (Read).
_handle_Data_Address_Read:
	rjmp $

	ORG 0x038
	// Data Address (Write).
_handle_Data_Address_Write:
	rjmp $

	ORG 0x03C
	// DTLB Protection (Read).
_handle_DTLB_Protection_Read:
	rjmp $

	ORG 0x040
	// DTLB Protection (Write).
_handle_DTLB_Protection_Write:
	rjmp $

	ORG 0x044
	// DTLB Modified: UNUSED IN AVR32UC.
_handle_DTLB_Modified:
	rjmp $

	ORG 0x050
	// ITLB Miss.
_handle_ITLB_Miss:
	rjmp $

	ORG 0x060
	// DTLB Miss (Read).
_handle_DTLB_Miss_Read:
	rjmp $

	ORG 0x070
	// DTLB Miss (Write).
_handle_DTLB_Miss_Write:
	rjmp $

	ORG 0x100
	// Supervisor Call.
_handle_Supervisor_Call:
	rjmp $


	/*
	 * Interrupt support.
	 * The interrupt controller must provide the offset address relative to
	 * EVBA.
	 * Important note:
	 * All interrupts call a C function named _get_interrupt_handler.
	 * This function will read group and interrupt line number to then
	 * return in R12 a pointer to a user-provided interrupt handler.
	 */
	ALIGN 2

	REPTI   priority, 0, 1, 2, 3
	// Export symbol.
	PUBLIC  ??INT<priority>
	PUBLIC  _int<priority>
??INT<priority>:
_int<priority>:
#if __CORE__ == __AVR32A__
	/*
	 * R8-R12, LR, PC and SR are automatically pushed onto the system stack
	 * by the CPU upon interrupt entry. No other register is saved by
	 * hardware.
	 */
#elif __CORE__ == __AVR32B__
	/*
	 * PC and SR are automatically saved in respectively RAR_INTx and
	 * RSR_INTx by the CPU upon interrupt entry. No other register is saved
	 * by hardware.
	 */
	pushm   r8-r12, lr
#endif
	// Pass the int_level parameter to the _get_interrupt_handler function.
	mov     r12, priority
	mcall   __get_interrupt_handler
	// Get the pointer to the interrupt handler returned by the function.
	cp.w    r12, 0
#if __CORE__ == __AVR32A__
	/*
	 * If this was not a spurious interrupt (R12 != NULL), jump to the
	 * handler.
	 */
	movne   pc, r12
#elif __CORE__ == __AVR32B__
	// If this was a spurious interrupt (R12 == NULL), branch.
	breq    spint<priority>
	/*
	 * Push the pointer to the interrupt handler onto the system stack since
	 * no register may be altered.
	 */
	st.w    --sp, r12
	popm    r8-r12, lr, pc// Restore registers and jump to the handler.
spint<priority>:
	popm    r8-r12, lr
#endif
	/*
	 * If this was a spurious interrupt (R12 == NULL), return from event
	 * handler.
	 */
	rete
	ENDR


	// Constant data area.
	ALIGN 2

	// Import symbols.
	EXTERN  eic_nmi_handler
	EXTERN  _get_interrupt_handler
__eic_nmi_handler:
	DC32  eic_nmi_handler
__get_interrupt_handler:
	DC32  _get_interrupt_handler


	END


//! \endverbatim
//! @}
